#!/bin/csh

#
# Install script for the load administration product (loadadmin)
#
# Usage:  Install
#
cd `dirname $0` && source ./Configuration

#
# Create a symbolic link to the configuration file in each sub-directory.
#
# NOTE: The adhoc directory is excluded because it has its own configuration
#       file and the adhoc load does not use mgiconfig.
#
foreach i (bin dev jboss prod public robot)
    cd $i
    rm -f Configuration
    ln -s ../Configuration .
    cd ..
end

#
# Make sure the log directory exists.
#
if ( ! -d ${LOGSDIR} ) then
    mkdir -p ${LOGSDIR}
endif

#
# Set permissions.
#
chmod 754 Configuration Install

foreach i (adhoc bin dev jboss prod public robot)
    chmod -fR 754 $i
    chgrp -fR progs $i
end

#
# Skip public and robot Fewi servers where there is no webster user.
#
switch (${SERVER_NAME})
    case mgi-pubfewi1:
    case mgi-pubfewi2:
    case mgi-botfewi1:
    case mgi-botfewi2:
        exit 0
        breaksw
    default:
        breaksw
endsw

chmod 700 bin/startTomcat bin/stopTomcat bin/restartTomcat bin/updateImsr*
chgrp www bin/startTomcat bin/stopTomcat bin/restartTomcat bin/updateImsr*
chown webster bin/startTomcat bin/stopTomcat bin/restartTomcat bin/updateImsr*
